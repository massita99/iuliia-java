plugins {
    id 'java-library'
    id 'groovy-base'
    id "io.freefair.lombok" version "5.0.0"
    id 'jacoco'
    id 'maven-publish'
    id 'signing'
}

group 'io.github.massita99'
version '0.2'

repositories {
    mavenCentral()
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
    withJavadocJar()
    withSourcesJar()
}

ext{
    //dependencies
    gsonVersion = '2.8.6'
    apacheCommonsVersion = '3.6'
    groovyVersion = '2.5.7'
    spockVersion = '1.3-groovy-2.5'
}

dependencies {
    implementation "com.google.code.gson:gson:$gsonVersion"
    implementation "org.apache.commons:commons-lang3:$apacheCommonsVersion"

    testImplementation "org.codehaus.groovy:groovy-all:$groovyVersion"
    testImplementation "org.spockframework:spock-core:$spockVersion"
}

task copySchemas(type: Copy) {
    from 'schemas'
    into 'src/main/resources/schemas'
    include '**/*.json'
}

processResources.dependsOn(copySchemas)

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled false
    }
}

check.dependsOn jacocoTestReport

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'iuliia-java'
            from components.java
            pom {
                name = 'Iuliia-java'
                description = 'Transliterate Cyrillic â†’ Latin in every possible way'
                url = 'https://github.com/massita99/iuliia-java'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'massita99'
                        name = 'Max Kilin'
                        email = 'max.kilin@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/massita99/iuliia-java.git'
                    developerConnection = 'scm:git:https://github.com/massita99/iuliia-java.git'
                    url = 'https://github.com/massita99/iuliia-java'
                }
            }
        }
    }
    repositories {
        maven {
            // change URLs to point to your repos, e.g. http://my.org/repo
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username ossrhUsername
                password ossrhPassword
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}